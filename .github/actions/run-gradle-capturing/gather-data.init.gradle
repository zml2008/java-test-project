import groovy.transform.Canonical
import groovy.transform.Immutable

initscript {
  repositories {
    maven {
      name 'jenkins-repo'
      url 'https://repo.jenkins-ci.org/releases/'
      mavenContent {
        releasesOnly()
        includeGroup 'edu.hm.hafner'
      }
    }
    mavenCentral()
  }

  dependencies {
    classpath "edu.hm.hafner:analysis-model:10.9.4"
  }
}

// Collect paths
def projectDirs = java.util.concurrent.ConcurrentHashMap.newKeySet()
def testExecutions = java.util.concurrent.ConcurrentHashMap.newKeySet()
def checkstyleReports = java.util.concurrent.ConcurrentHashMap.newKeySet()
def reviewdogJsons = java.util.concurrent.ConcurrentHashMap.newKeySet()

class StdoutCapture extends edu.hm.hafner.analysis.ReaderFactory implements StandardOutputListener {
  final String taskName
  final StringWriter out

  StdoutCapture(final String taskName, final StringWriter out) {
    super(java.nio.charset.StandardCharsets.UTF_8)

    this.taskName = taskName
    this.out = out
  }

  @Override
  void onOutput(final CharSequence output) {
    out.append(output)
  }

  @Override
  String getFileName() {
    return this.taskName
  }

  @Override
  Reader create() {
    return new StringReader(this.out.toString())
  }
}

@Canonical
final class Diagnostic {
  String message
  Location location
  Severity severity // optional
  Source source
  Code code // optional
  List<Suggestion> suggestions // optional
  String original_output
}

enum Severity {
  UNKNOWN_SEVERITY,
  ERROR,
  WARNING,
  INFO,
}

@Immutable(copyWith = true)
class Location {
  String path; // absolute or relative file path
  Range range; // optional
}

@Immutable(copyWith = true)
class Source {
  String name;
  String url;
}

@Immutable(copyWith = true) 
class Code {
  String value;
  String url;
}

@Immutable(copyWith = true)
class Suggestion {
  Range range; // where suggestion applies
  String text; // empty string to delete
}

@Immutable(copyWith = true)
class Range {
  Position start;
  Position end; // optional, exclusive
}

@Immutable(copyWith = true)
class Position {
  int line
  int column
}


class TaskCaptureListener implements TaskExecutionListener {
  private static final Logger LOGGER = Logging.getLogger("github-actions");
  private static final java.util.regex.Pattern STACKTRACE_LINE = ~/(?<loader>\/)?(?<module>(?<version>@)?\/)?package.class.method\((?<source>[^:]+):(?<lineno>[\d]+)\)/
  private static final groovy.json.JsonGenerator jsonGenerator = new groovy.json.JsonGenerator.Options()
    .excludeNulls()
    .excludeFieldsByName('contentHash', 'originalClassName')
    .build()
  def parserRegistry = new edu.hm.hafner.analysis.registry.ParserRegistry()
  private final ThreadLocal<StandardOutputListener> state = ThreadLocal.withInitial { null }
  private final Collection<File> reviewdogJsons;

  TaskCaptureListener(final Collection<File> reviewdogJsons) {
    this.reviewdogJsons = reviewdogJsons
  }

  @Override
  void beforeExecute(Task task) {
    if (task instanceof AbstractCompile && task.hasProperty('outputReviewdogJson') && task.hasProperty('expectedReporters')) {
      def listener = new StdoutCapture(task.path, new StringWriter())
      // task.logging.addStandardOutputListener(listener)
      task.logging.addStandardErrorListener(listener)
      state.set(listener)
    }
  }

  @Override
  void afterExecute(final Task task, final TaskState state) {
    if (task instanceof AbstractCompile) {
      def oldCapture = this.state.get()
      this.state.remove()
      if (oldCapture != null) {
        // task.logging.removeStandardOutputListener(oldCapture)
        task.logging.removeStandardErrorListener(oldCapture)

        def parsers = task.expectedReporters.collect { parserRegistry.get(it).createParser() }
        def report = new edu.hm.hafner.analysis.Report(task.path, task.name)
        parsers.each { parser ->
          if (parser.accepts(oldCapture)) {
            report.addAll(parser.parse(oldCapture))
          }
        }
        // read contents of log
        logReportMessages(report)
        task.outputReviewdogJson.parentFile.mkdirs()
        task.outputReviewdogJson.withWriter('UTF-8') { writer ->
          reportToRdJson(report).each {
            writer.writeLine it
          }
        }
      }
    }

    if (task instanceof Test) {
      // prepare JUnit report, fill in file names if possible
      def reportDir = task.reports.junitXml.outputLocation.get().asFile
      def classpath = new File(reportDir, 'test-classpath.txt')
      if (!classpath.exists()) {
        return
      }

      def knownFiles = classpath.readLines('UTF-8').collect { new File(it) }

      def diagnostics = []
      reportDir.eachFileMatch groovy.io.FileType.FILES, ~/.*\.xml$/, { file ->
        println "Reading: $file"
        def slurp = new XmlSlurper().parse(file)
        if (slurp.testcase.'*'.failure || slurp.testcase.'*'.'system-out') {
          println "Writing for $file"
          
          // we have to actually write something
        }
      }

      logReportMessages(report)
      def reviewDog = new File(reportDir, 'reviewdog.jsonl')
      reviewDog.withWriter('UTF-8') { writer -> 
        reportToRdJson(report).each {
          writer.writeLine it
        }
      }
      reviewdogJsons << reviewDog
    }
  }

  Collection<String> reportToRdJson(final edu.hm.hafner.analysis.Report report) {
    return report.collect({ issue -> 
      new Diagnostic(
        message: String.join('\n', issue.message, issue.description),
        location: new Location(
          path: issue.absolutePath,
          range: new Range(
            start: new Position(issue.lineStart, issue.columnStart),
            end: new Position(issue.lineEnd, issue.columnEnd)
          )
        ),
        severity: severityToRd(issue.severity),
        source: new Source(
          name: undefinedToNull(issue.originName)
        ),
        code: new Code(
          value: undefinedToNull(issue.type)
        )
      )
    }).collect { jsonGenerator.toJson(it) }
  }

  private Severity severityToRd(final edu.hm.hafner.analysis.Severity severity) {
    switch (severity.name) {
      case 'ERROR': 
      case 'HIGH':
        return Severity.ERROR
      case 'NORMAL':
        return Severity.WARNING
      case 'LOW':
      case 'INFO':
        return Severity.INFO
      default:
        return Severity.UNKNOWN_SEVERITY
    }
  }

  private String undefinedToNull(final String input) {
    if ('-'.equals(input)) {
      return null
    }
    return input
  } 

  void logReportMessages(final edu.hm.hafner.analysis.Report report) {
    report.infoMessages.each {
      LOGGER.warn "::notice ::$it".toString()
    }

    report.errorMessages.each {
       LLOGER.warn "::error ::$it".toString()
    }
  }
}

// Capture task output to a file
def taskCapture = new TaskCaptureListener(reviewdogJsons)
gradle.addListener taskCapture

gradle.afterProject { project ->
  // Capture project directory
  projectDirs << project.projectDir

  // Log test task output
  project.tasks.withType(Test) {
    def testClassList = reports.junitXml.outputLocation.file('test-classpath.txt')
    outputs.file(testClassList)
    reports {
      junitXml {
        // Make sure we're enabled
        required = true
      }
    }

    doFirst {
      // Write all test class directories to a file in the report discord
      def testClassListFile = testClassList.get().asFile
      testExecutions.add(testClassListFile.parentFile)

      // The JUnit XML doesn't give us actual file names, so we have to link up the source files used to build these test tasks with the actual classpath
      def inputTasks = new java.util.ArrayDeque<Task>(testClassesDirs.buildDependencies.getDependencies(it))

      testClassListFile.withWriter('utf-8') { writer -> 
        Task next = null;
        while ((next = inputTasks.poll()) != null) {
          if (next instanceof SourceTask) {
            next.source.each { file -> 
                writer.writeLine file.absolutePath
            }
          } else {
            inputTasks.addAll next.taskDependencies.getDependencies(next)
          }
        }
      }
    }
  }

  // Capture checkstyle output
  project.tasks.withType(Checkstyle) {
    reports.xml.required = true

    doFirst {
      checkstyleReports << reports.xml.outputLocation.get().asFile
    }
  }

  // capture output of javac
  def layout = project.layout
  project.tasks.withType(JavaCompile) {
    it.ext.outputReviewdogJson = layout.buildDirectory.file("java-compile-output/${it.name}.jsonl").get().asFile
    it.ext.expectedReporters = ['error-prone', 'java'] // https://github.com/jenkinsci/analysis-model/blob/master/SUPPORTED-FORMATS.md
    doFirst {
      reviewdogJsons << it.outputReviewdogJson
    }
  }
}

// Provide JSON data to subsequent tasks
gradle.buildFinished {
    def json = groovy.json.JsonOutput.toJson([
      version: gradle.rootProject.version,
      projectDirs: projectDirs.collect { it.absolutePath },
      testExecutions: testExecutions.collect { it.absolutePath },
      checkstyleReports: checkstyleReports.collect { it.absolutePath },
      reviewdogJsons: reviewdogJsons.collect { it.absolutePath }
    ])
    logger.warn("::set-output name=project-metadata::${json}".toString())
}